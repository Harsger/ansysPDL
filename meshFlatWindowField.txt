FINISH 
/CLEAR,START

/PREP7 
! No polynomial elements
/PMETH,OFF,1

! Set electric preferences
KEYW,PR_ELMAG,1
KEYW,MAGELC,1

!!!!!!!!!!!!!!!Defining variables all in mm!!!!!!!!!!!!!!!!!!!

! geometry

amplification=0.120
drift=0.5

pitch=0.425
stripwidth=0.3
stripheight=0.035
kaptonheight=0.025

wireWidth=0.018
wirePitch=0.063
wireDist=0.0001

! number of unitcells
rowsx=3                          ! only odd numbers
rowsy=1                          ! only odd numbers

!voltages
driftvolt=-300
meshvolt=0
stripvolt=540

!!!!!!!!!!!!!!!Defining variables all in mm END!!!!!!!!!!!!!!!!!!!

midx=(rowsx-1)/2
midy=(rowsy-1)/2

meshheight=amplification

wireRadius=wireWidth/2
wireCell=wirePitch*2
meshPerCrossing=pitch/wireCell
meshCenter=(meshPerCrossing-1)/2

!!!!!!!!!!!!!!!!!!!!!!materials!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!Define elementtype
!ET,1,SOLID122
ET,1,SOLID123

!Define materialpropertys
MP,PERX,1,1E10 ! metal
MP,PERX,2,4.0  ! FR4
MP,PERX,3,1.0  ! gas 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!materials END!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!!!!!!!geometry!!!!!!!!!!!!!!!!!!!!!!!!!

! ********** mesh *******************

xoffset=-wirePitch/2
yoffset=-wirePitch/2

! cylinder

WPLANE,,xoffset+wirePitch,yoffset,meshheight+wireRadius,xoffset+wirePitch,yoffset,meshheight+wireRadius-1,xoffset+wirePitch,yoffset+1,meshheight+wireRadius
CYL4,0,0,wireRadius,,,,-wirePitch
CSYS,0
WPLANE,,0,0,0,1,0,0,0,1,0

! rotate wire to create single window

xoffset=xoffset+wirePitch/2
yoffset=yoffset+wirePitch/2

! xoffset=-wirePitch/2
! yoffset=-wirePitch/2

WPLANE,,xoffset,yoffset,0,xoffset+1,yoffset,0,xoffset,yoffset+1,0
CSWPLA,11,CYLIN
VGEN,4,1,,,,90
CSYS,0
WPLANE,,0,0,0,1,0,0,0,1,0
CSWPLA,12,CART

! asign material for distinction of gas and wires

ALLSEL,ALL

VATT,1,,1
/COLOR,VOLU,ORAN,ALL

! gas in window
BLOCK,xoffset-wirePitch/2,xoffset+wirePitch/2,yoffset-wirePitch/2,yoffset+wirePitch/2,0,meshheight+wireWidth+drift

! asign material for distinction of gas and wire

ALLSEL,ALL

VSEL,U,MAT,,1
! VSEL,S,LOC,Z,meshheight+5*wireRadius,meshheight+5*wireRadius+drift
*GET,gasindex,VOLU,,NUM,MAX
VATT,3,,1
/COLOR,VOLU,GREE,ALL

! subtract wirevolume from gasvolume

ALLSEL,ALL

VSBV,gasindex,ALL,,DELETE,KEEP

ALLSEL,ALL

VSEL,U,MAT,,1
! VSEL,S,LOC,Z,meshheight+5*wireRadius,meshheight+5*wireRadius+drift
*GET,gasindex,VOLU,,NUM,MAX
VATT,3,,1
/COLOR,VOLU,GREE,ALL
ASLV,S
AATT,3,,1
ASEL,U,LOC,Z,meshheight+wireWidth,meshheight+wireWidth+drift
ASEL,U,LOC,Z,0,meshheight
AATT,1,,1
/COLOR,AREA,ORAN,ALL

!!!!!!!!!!!!!!!!!!!!geometry END!!!!!!!!!!!!!!!!!!!!!!!!!

ALLSEL,ALL

ASEL,S,LOC,Z,0
AATT,1,,1
/COLOR,AREA,ORAN,ALL

ALLSEL,ALL

ASEL,S,LOC,Z,meshheight+wireRadius+drift
AATT,1,,1
/COLOR,AREA,ORAN,ALL

ALLSEL,ALL

VSEL,S,MAT,,1
ASLV,S
AATT,1,,1

ALLSEL,ALL

divisions=20

horizontalSteps=wirePitch/divisions
verticalSteps=meshheight/divisions

side1=xoffset-wirePitch/2

ASEL,S,LOC,X,side1
ASEL,R,LOC,Z,0,meshheight
*GET,area1,AREA,,NUM,MAX

side2=xoffset+wirePitch/2

ASEL,S,LOC,X,side2
ASEL,R,LOC,Z,0,meshheight
*GET,area2,AREA,,NUM,MAX

side3=yoffset-wirePitch/2

ASEL,S,LOC,Y,side3
ASEL,R,LOC,Z,0,meshheight
*GET,area3,AREA,,NUM,MAX

side4=yoffset+wirePitch/2

ASEL,S,LOC,Y,side4
ASEL,R,LOC,Z,0,meshheight
*GET,area4,AREA,,NUM,MAX

ALLSEL,ALL

*DO,cx,0,divisions-1,1

*DO,cy,0,divisions-1,1

HPTCREATE,AREA,area1,,COORD,side1,yoffset-wirePitch/2+(cx+0.5)*horizontalSteps,(cy+0.5)*verticalSteps

HPTCREATE,AREA,area2,,COORD,side2,yoffset-wirePitch/2+(cx+0.5)*horizontalSteps,(cy+0.5)*verticalSteps

HPTCREATE,AREA,area3,,COORD,xoffset-wirePitch/2+(cx+0.5)*horizontalSteps,side3,(cy+0.5)*verticalSteps

HPTCREATE,AREA,area4,,COORD,xoffset-wirePitch/2+(cx+0.5)*horizontalSteps,side4,(cy+0.5)*verticalSteps

*ENDDO

*ENDDO

!!!!!!!!!!!!!!!!!!!!meshing!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

VSEL,ALL
VSEL,U,MAT,,1

VPLOT     
SMRT,1  
!MSHAPE,0,3D 
!MSHKEY,0 
VMESH,ALL 
FINISH  
/SOL

!!!!!!!!!!!!!!!!!!!!!!!!!!!meshing END!!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!!!!!!constraints!!!!!!!!!!!!!!!!!!!!!!!

ALLSEL,ALL

ASEL,S,LOC,X,xoffset-wirePitch/2
ASEL,R,MAT,,3
DA,ALL,SYMM

ALLSEL,ALL

ASEL,S,LOC,X,xoffset+wirePitch/2
ASEL,R,MAT,,3
DA,ALL,SYMM

ALLSEL,ALL

ASEL,S,LOC,Y,yoffset-wirePitch/2
ASEL,R,MAT,,3
DA,ALL,SYMM

ALLSEL,ALL

ASEL,S,LOC,Y,yoffset+wirePitch/2
ASEL,R,MAT,,3
DA,ALL,SYMM

ALLSEL,ALL

VSEL,S,MAT,,3
ASLV,S
ASEL,U,LOC,Z,meshheight+wireWidth,meshheight+wireWidth+drift
ASEL,U,LOC,Z,0,meshheight
DA,ALL,VOLT,meshvolt

ALLSEL,ALL

ASEL,S,LOC,Z,0
DA,ALL,VOLT,stripvolt

ALLSEL,ALL

ASEL,S,LOC,Z,meshheight+wireWidth+drift
DA,ALL,VOLT,driftvolt

ALLSEL,ALL

!!!!!!!!!!!!!!!!!!!constraints END!!!!!!!!!!!!!!!!!

ALLSEL,ALL

! Solve *********************
/SOLU
SOLVE   
FINISH

/POST1 

! Plot voltage
/EFACET,1  
PLNSOL,VOLT,,0

!specify range
/CONTOUR,1,126,AUTO
!/CONTOUR,1,27,0,,1 
/REPLOT 


!!!!!!!!!!!!!!!!!!!!geometry END!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
